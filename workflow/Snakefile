from snakemake.utils import min_version

##### set minimum snakemake version #####
min_version("7.8.5")

##### setup report ########
configfile: "config/config.yaml"

##### load rules ##########
include: "rules/common.smk"

crams = pd.read_csv(config["crams"], sep=",", dtype={"sample": str, "cram_location": str}).set_index("sample")
FILES = crams.to_dict()["cram_location"]
SAMPLES = crams.index.tolist()

wildcard_constraints:
    sample = "|".join(SAMPLES)

rule all:
    input:
        "results/hiPhivePrioritiser.html",
        "results/phenixPrioritiser.html",
        "results/phivePrioritiser.html",
        "results/exomeWalkerPrioritiser.html"

rule samtools_extract:
    input:
        lambda wildcards: FILES[wildcards.sample]
    output:
        fq1 = "input_reads/{sample}_1.fq.gz",
        fq2 = "input_reads/{sample}_2.fq.gz",
        fq  = temp("input_reads/{sample}.fq.gz")
    log:
        "logs/extract/{sample}.log"
    threads:
        4
    conda:
        "envs/samtools_extract.yaml"
    resources:
        mem_mb = get_mem_mb,
        runtime = 60
    shell:
        "samtools sort -n -@ {threads} {input} | "
        "samtools fastq -@ {threads} - "
        "-1 {output.fq1} "
        "-2 {output.fq2} "
        "-s {output.fq} > /dev/null"

rule bwa_align:
    input:
        fq1 = "input_reads/{sample}_1.fq.gz",
        fq2 = "input_reads/{sample}_2.fq.gz",
        reference = config["reference"]
    output:
        "namesorted_alignments/{sample}.alignment.bam"
    log:
        "logs/align/{sample}.log"
    conda:
        "envs/bwa_align.yaml"
    params:
        bwa_params="\"@RG\\tID:{sample}\\tSM:{sample}\\tPL:illumina\"",
    threads:
        10
    resources:
        mem_mb=get_mem_mb,
        runtime= 600
    shell:
        "bwa mem -t {threads} -Y -R {params.bwa_params} -K 100000000 "
        "{input.reference} {input.fq1} {input.fq2} | "
        "samblaster --addMateTags | "
        "samtools view -b -@ {threads} -o {output} -"

rule samtools_sortindex:
    input:
        "namesorted_alignments/{sample}.alignment.bam"
    output:
        bam = "alignments/{sample}.alignment.bam",
        bai = "alignments/{sample}.alignment.bam.bai"
    log:
        "logs/index/{sample}.log"
    threads:
        10
    conda:
        "envs/samtools_sortindex.yaml"
    resources:
        mem_mb=get_mem_mb,
        runtime=600
    shell:
        """
        samtools sort -T {wildcards.sample} -@ {threads} -O BAM -o {output.bam} {input}
        samtools index {output.bam}
        """

rule alignstats_stats:
    input:
        "alignments/{sample}.alignment.bam"
    output:
        "alignments/{sample}.stats"
    log:
        "logs/stats/{sample}.log"
    threads:
        1
    conda:
        "envs/alignstats_stats.yaml"
    resources:
        mem_mb=get_mem_mb,
        runtime=300
    params:
        alignstat_target=config["target_bed"],
    shell:
        "alignstats -i {input} -o {output} "
        "-p -W -P {threads} -t {params.alignstat_target} "

rule freebayes_callvariants:
    input:
        bam = expand("alignments/{sample}.alignment.bam", sample=SAMPLES),
        reference = config["reference"],
        stats = expand("alignments/{sample}.stats", sample=SAMPLES)
    output:
        "variants/variants.vcf"
    log:
        "logs/variants/variants.log"
    threads:
        20
    conda:
        "envs/freebayes_callvariants.yaml"
    resources:
        mem_mb=get_mem_mb,
        runtime=2880
    params:
        target_bed=config["target_bed"],
    shell:
        """
        freebayes-parallel <(awk '{{print $1":"$2"-"$3}}' {params.target_bed}) {threads} --fasta-reference {input.reference} {input.bam}  > variants/variants.vcf
        """

# add script to improve filtering. Set genotypes as missing on sex chromosomes
# if there is a violation of ploidy. Also investigate some ML methods for 
# filtering freebayes calls that can leverage familial relationships.
rule vcffilter_filter:
    input:
        "variants/variants.vcf"
    output:
        "variants/variants.flt.vcf"
    log:
        "logs/variants/variants.flt.log"
    threads:
        1
    conda:
        "envs/vcffilter_filter.yaml"
    resources:
        mem_mb=get_mem_mb,
        runtime=240
    shell:
        """
        vcffilter -f "QUAL > 1 & QUAL / AO > 10 & SAF > 0 & SAR > 0 & RPR > 1 & RPL > 1" -t "PASS" -F "FAIL" {input} > {output}
        """

rule bgzip_zip:
    input:
        "variants/variants.flt.vcf"
    output:
        "variants/variants.flt.vcf.gz"   
    log:
        "logs/variants/bgzip.log"
    threads:
        4
    conda:
        "envs/bgzip_zip.yaml"
    resources:
        mem_mb=get_mem_mb,
        runtime=60
    shell:      
        "bgzip -@ {threads} {input}"

rule tabix_index:
    input:
        "variants/variants.flt.vcf.gz"
    output:
        "variants/variants.flt.vcf.gz.tbi"
    log:
        "logs/variants/tabix.log"
    threads:
        1
    conda:
        "envs/tabix_index.yaml"
    resources:
        mem_mb=get_mem_mb,
        runtime=60
    shell:
        "tabix {input}"

rule bcftools_annt:
    input:
        vcf="variants/variants.flt.vcf.gz",
        index="variants/variants.flt.vcf.gz.tbi"
    output:
        "variants/variants.flt.ann.vcf.gz"
    log:
        "logs/variants/lcr.log"
    threads:
        1
    conda:
        "envs/bcftools_annt.yaml"
    resources:
        mem_mb=get_mem_mb,
        runtime=120
    params:
        lcr_bed=config["LCR_bed"]
    shell:
        """
        bcftools annotate -a {params.lcr_bed} -h <(echo "##INFO=<ID=LCR,Number=0,Type=Flag,Description=\"Variant overlaps a low-complexity region as determined using DUST\">") -c CHROM,FROM,TO,LCR {input.vcf} -Oz -o {output}
        """

use rule tabix_index as tabix_index_ann with:
    input:
        "variants/variants.flt.ann.vcf.gz"
    output:
        "variants/variants.flt.ann.vcf.gz.tbi"
    log:
        "logs/variants/tabix.ann.log"


rule variantqc_stats:
    input:
        vcf="variants/variants.flt.ann.vcf.gz",
        ped=config["ped"],
        reference=config["reference"],
        vcfi="variants/variants.flt.ann.vcf.gz.tbi"
    output:
        "variants/variant_qc.html"
    log:
        "logs/variants/variantqc.log"
    threads:
        2
    conda:
        "envs/variantqc_stats.yaml"
    resources:
        mem_mb=get_mem_mb,
        runtime=60
    params:
        variantqc_jar=config["DISCVRSeq"]
    shell:
        "java -jar {params.variantqc_jar} VariantQC -R {input.reference} -ped {input.ped} -V {input.vcf} -O {output} && rm DISCVRSeq-1.3.27.jar"

rule vt_decnorm:
    input:
        vcf="variants/variants.flt.ann.vcf.gz",
        stats="variants/variant_qc.html",
        reference=config["reference"]
    output:
        "variants/variants.flt.ann.vt.vcf.gz"
    log:
        "logs/variants/vt.log"
    threads:
        2
    conda:
        "envs/vt_decnorm.yaml"
    resources:
        mem_mb=get_mem_mb,
        runtime=120
    shell:
        "vt decompose -s {input.vcf} | "
        "vt normalize -r {input.reference} - | "
        "bgzip -c > {output}"

use rule tabix_index as tabix_index_vt with:
    input:
        "variants/variants.flt.ann.vt.vcf.gz"
    output:
        "variants/variants.flt.ann.vt.vcf.gz.tbi"
    log:
        "logs/variants/tabix.vt.log"

rule triodenovo_find:
    input:
        "variants/variants.flt.ann.vt.vcf.gz"
    output:
        "variants/denovo_variants.flt.vt.ann.vcf"
    log:
        "logs/variants/denovo.log"
    threads:
        1
    resources:
        mem_mb=get_mem_mb,
        runtime=300
    params:
        triodenovo_bin=config["triodenovo"],
        ped_file=config["ped"],
        xchrom=config["xchrom"],
        ychrom=config["ychrom"]
    shell:
        """
        {params.triodenovo_bin} --ped {params.ped_file} --in_vcf {input} --out_vcf {output} --minDepth 10 --chrX {params.xchrom}
        """

rule denovo_filter:
    input:
        vcf="variants/denovo_variants.flt.vt.ann.vcf",
        ped=config["ped"]
    output:
        vcf="variants/filtered_denovo_variants.flt.vt.ann.vcf"
    log:
        "logs/variants/denovo_filter.log"
    threads:
        1
    conda:
        "envs/denovo_filter.yaml"
    resources:
        mem_mb=get_mem_mb,
        runtime=60
    shell:
        "python workflow/scripts/denovo_filter.py {input.vcf} {input.ped} {output.vcf}"

rule bcftools_annt_denovo:
    input:
        vcf="variants/variants.flt.ann.vt.vcf.gz",
        denovo="variants/filtered_denovo_variants.flt.vt.ann.vcf"
    output:
        "variants/final.vcf.gz"
    log:
        "logs/variants/final.log"
    threads:
        1
    conda:
        "envs/bcftools_annt_denovo.yaml"
    resources:
        mem_mb=get_mem_mb,
        runtime=120
    shell:
        """
        bcftools query -f '%CHROM\t%POS\t1\n' {input.denovo} | bgzip -c > variants/variants.pos.gz
        tabix -s1 -b2 -e2 variants/variants.pos.gz
        bcftools annotate -a variants/variants.pos.gz -h <(echo "##INFO=<ID=DENOVO,Number=0,Type=Flag,Description=\"A denovo variant that passed filters\">") -c CHROM,POS,DENOVO {input.vcf} -Oz -o {output}
        rm variants/variants.pos.gz variants/variants.pos.gz.tbi
        """

use rule tabix_index as tabix_index_final with:
    input:
        "variants/final.vcf.gz"
    output:
        "variants/final.vcf.gz.tbi"
    log:
        "logs/variants/tabix.final.log"

# Right now this dumps a string with the right config to a file, but I should
# change this to a actual YML config
rule exomiser_create_config:
    input:
        reference = config["reference"],
        vcf = "variants/final.vcf.gz",
        vcfi = "variants/final.vcf.gz.tbi",
        ped = config["ped"],
        hpo = config["hpo"]
    output:
        "exomiser/hiPhive_config.yml"
    log:
        "logs/exomiser/hiPhive_config.log"
    threads:
        1
    conda:
        "envs/exomiser_create_config.yaml"
    resources:
        mem_mb = get_mem_mb,
        runtime = 60 
    params:
        prioritiser = "hiPhivePrioritiser",
        priority = "HIPHIVE_PRIORITY"
    shell:
        """
        echo "python workflow/scripts/exomiser_create_config.py {input.reference} {input.vcf} {input.vcfi} {input.ped} {input.hpo} {params.prioritiser} {params.priority} {output}"
        python workflow/scripts/exomiser_create_config.py {input.reference} {input.vcf} {input.vcfi} {input.ped} {input.hpo} {params.prioritiser} {params.priority} {output}
        """

use rule exomiser_create_config as exomiser_create_phenix_config with:
    output:
        "exomiser/phenix_config.yml"
    log:
        "logs/exomiser/phenix_config.log"
    params:
        prioritiser = "phenixPrioritiser",
        priority = "PHENIX_PRIORITY"

use rule exomiser_create_config as exomiser_create_phive_config with:
    output:
        "exomiser/phive_config.yml"
    log:
        "logs/exomiser/phive_config.log"
    params:
        prioritiser = "phivePrioritiser",
        priority = "PHIVE_PRIORITY"

# currently the seeds include genes from clinVar that are pathogenic, implicated
# in heterotaxy and lead to a protein change. This should read from a data
# file that can easily be updated. The ids are ENTREZ
use rule exomiser_create_config as exomiser_create_exomeWalker_config with:
    output:
        "exomiser/exomeWalker_config.yml"
    log:
        "logs/exomiser/exomeWalker_config.log"
    params:
        prioritiser = "exomeWalkerPrioritiser",
        priority = "PHIVE_PRIORITY"


rule exomiser_prioritize:
    input:
        "exomiser/hiPhive_config.yml"
    output:
        "results/hiPhivePrioritiser.html"
    log:
        "logs/exomiser/hiPhive_exomiser.log"
    threads:
        2
    conda:
        "envs/exomiser_prioritize.yaml"
    resources:
        mem_mb = get_mem_mb,
        runtime = 120
    params:
        exomiser_jar=config["exomiser_jar"],
        exomiser_app_properties=config["exomiser_app"]
    shell:
        """
        java -Xmx4g -jar {params.exomiser_jar} --analysis {input} --spring.config.location={params.exomiser_app_properties}
        """

use rule exomiser_prioritize as exomiser_phenix_prioritize with:
    input:
        "exomiser/phenix_config.yml"
    output:
        "results/phenixPrioritiser.html"
    log:
        "logs/exomiser/phenix_exomiser.log"

use rule exomiser_prioritize as exomiser_phive_prioritize with:
    input:
        "exomiser/phive_config.yml"
    output:
        "results/phivePrioritiser.html"
    log:
        "logs/exomiser/phive_exomiser.log"

use rule exomiser_prioritize as exomiser_exomeWalker_prioritize with:
    input:
        "exomiser/exomeWalker_config.yml"
    output:
        "results/exomeWalkerPrioritiser.html"
    log:
        "logs/exomiser/exomeWalker_exomiser.log"

